buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath platform("org.grails:grails-bom:$grailsVersion")
        classpath "org.grails:grails-gradle-plugin"
    }
}

version project.projectVersion
group "org.grails.plugins"

apply plugin: "org.grails.grails-plugin"

apply from: rootProject.file('gradle/java-config.gradle')

ext {
    testMaxParallelFork = isCiBuild ? 1 : 4
    testForkEvery = isCiBuild ? 10 : 20
    testJvmArgs = ['-Xmx1536M']
}

// Fixes JSP tests
configurations.all {
    resolutionStrategy.dependencySubstitution.all { DependencySubstitution dependency ->
        if (dependency.requested instanceof ModuleComponentSelector) {
            if (group == 'org.grails' || group == 'org.grails.plugins') {
                def targetProject = findProject(":${dependency.requested.module}")
                if (targetProject != null) {
                    dependency.useTarget targetProject
                }
            }
        }
    }
}

dependencies {
    api project(":grails-web-gsp-taglib")
    api project(":grails-plugin-sitemesh3")

    runtimeOnly(project(":grails-web-jsp"))
    api "org.apache.commons:commons-text:$commonsTextVersion"
    api "org.grails:grails-plugin-codecs"
    astImplementation "org.grails:grails-web"
    astImplementation "org.grails:grails-plugin-controllers"

    testImplementation "jakarta.annotation:jakarta.annotation-api"
    testImplementation "jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api:$jstlVersion"
    testImplementation "jakarta.servlet.jsp:jakarta.servlet.jsp-api:$jspApiVersion"
    testImplementation "org.grails:grails-gorm-testing-support"
    testImplementation "org.grails:grails-testing-support"
    testImplementation "org.grails:grails-web-testing-support"

    testRuntimeOnly "org.glassfish.web:jakarta.servlet.jsp.jstl:$jstlVersion"
    testRuntimeOnly "org.grails.plugins:async"
    testRuntimeOnly "org.grails:grails-plugin-url-mappings"
}

apply from: rootProject.file('gradle/test.gradle')
apply from: rootProject.file('gradle/publish.gradle')
