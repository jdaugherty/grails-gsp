plugins {
    id 'com.gradle.develocity' version '3.19'
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '2.0.2'
}

def isCI = System.getenv('CI') != null
def isLocal = !isCI
def isAuthenticated = System.getenv('DEVELOCITY_ACCESS_KEY') != null

develocity {
    server = 'https://ge.grails.org'
    buildScan {
        tag('grails')
        tag('grails-gsp')
        publishing.onlyIf { isAuthenticated }
        uploadInBackground = isLocal
    }
}

buildCache {
    local { enabled = isLocal }
    remote(develocity.buildCache) {
        push = isCI && isAuthenticated
        enabled = true
    }
}

rootProject.name = 'grails-gsp'

include 'docs'
include 'grails-gsp'
include 'grails-plugin-gsp'
include 'grails-plugin-sitemesh3'
include 'grails-taglib'
include 'grails-web-gsp'
include 'grails-web-gsp-taglib'
include 'grails-web-jsp'
include 'grails-web-taglib'
include 'grails-web-testing-support'

// Examples
include 'examples-sitemesh3'

project(':examples-sitemesh3').projectDir = file('examples/sitemesh3')